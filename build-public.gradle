buildscript {
    repositories {
        jcenter()
    }
}

plugins {
    id "com.jfrog.bintray" version "1.8.4"
    id "com.jfrog.artifactory" version "4.7.5"
}

repositories {
    mavenCentral()
    jcenter()
}

task wrapper(type: Wrapper) {
    distributionType = Wrapper.DistributionType.ALL
    jarFile = "${project.projectDir}/gradle-public/wrapper/gradle-wrapper.jar"
    scriptFile = "${project.projectDir}/gradlew-public"
    gradleVersion = '4.10.2'
}

apply plugin: 'maven'
apply plugin: 'maven-publish'
apply plugin: 'java'
apply from: 'dependencies.gradle'

compileJava.options.encoding = 'UTF-8'
compileTestJava.options.encoding = 'UTF-8'
sourceCompatibility = '1.8'
targetCompatibility = '1.8'

jar {
    manifest {
        attributes(
                'Implementation-Version': "$project.version",
                'Implementation-Title': "$project.name",
                'Built-By': java.lang.System.getProperty('user.name'),
                'Built-Date': new Date().format('yyyy-MM-dd'),
                'Copyright': '2018 NBCO Yandex.Money LLC',
                'License': 'MIT'
        )
    }

    from(projectDir.absolutePath) {
        include "CHANGELOG.md", "README.md"
        into "META-INF"
    }
}

apply plugin: 'idea'
idea {
    project {
        jdkName = '1.8'
        languageLevel = '1.8'
        vcs = 'Git'
    }
    module {
        downloadJavadoc = true
        downloadSources = true
    }
}
test {
    systemProperty "file.encoding", "UTF-8"
    testLogging {
        events "skipped", "failed"
        exceptionFormat "full"
        showExceptions true
        showCauses true
        showStackTraces true
    }
}

apply plugin: "jacoco"
jacocoTestReport {
    reports {
        html.enabled = true
        xml.enabled = true
    }
}
check.dependsOn jacocoTestReport

javadoc {
    exclude "**/internal/**"
    options {
        docEncoding = 'UTF-8'
        charSet = 'UTF-8'
        encoding = 'UTF-8'
        failOnError = true
    }
}


task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar, javadocJar
}

publishing {
    publications {
        MyPublication(MavenPublication) {
            from components.java
            groupId 'com.yandex.money.tech'
            artifactId 'db-queue'
            version project.version
            artifact sourcesJar
            artifact javadocJar
        }
    }
}

bintrayUpload.onlyIf { !project.version.endsWith("-SNAPSHOT") }

bintray {
    user = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('BINTRAY_USER')
    key = project.hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : System.getenv('BINTRAY_API_KEY')
    publications = ['MyPublication']

    dryRun = false
    publish = true
    pkg {
        name = 'db-queue'
        repo = 'maven'
        userOrg = 'yandex-money-tech'
        desc = ' Worker-queue implementation on top of Java and database'
        websiteUrl = 'https://github.com/yandex-money-tech/db-queue'
        issueTrackerUrl = 'https://github.com/yandex-money-tech/db-queue/issues'
        vcsUrl = 'https://github.com/yandex-money-tech/db-queue.git'
        licenses = ['MIT']
        labels = ['postgresql', 'queue', 'java', 'database']
        publicDownloadNumbers = true
        githubRepo = 'yandex-money-tech/db-queue'
        githubReleaseNotesFile = 'README.md'
        version {
            name = project.version
            released = new Date()
            vcsTag = project.version
        }
    }
}

artifactory {
    contextUrl = 'http://oss.jfrog.org'
    publish {
        repository {
            repoKey = 'oss-snapshot-local'
            username = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('BINTRAY_USER')
            password = project.hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : System.getenv('BINTRAY_API_KEY')
        }
        defaults {
            publications('MyPublication')
            publishArtifacts = true
            publishPom = true
        }
    }
}

artifactoryPublish.onlyIf { project.version.endsWith("-SNAPSHOT") }